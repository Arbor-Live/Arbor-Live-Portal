struct PresentUniforms {
  // 1 keeps scene alpha (dark theme over black). 0 forces pure additive
  // compositing so a pale CSS fill is never punched into dark holes.
  alphaScale: f32,
  _pad0: f32,
  _pad1: f32,
  _pad2: f32,
};

@group(0) @binding(0) var<uniform> u: PresentUniforms;
@group(0) @binding(1) var sceneHDR: texture_2d<f32>;
@group(0) @binding(2) var bloomTexture: texture_2d<f32>;
@group(0) @binding(3) var linearSampler: sampler;

fn LinearTosRGB(value: vec4f) -> vec4f {
  let lt = value.rgb * 12.92;
  let gt = 1.055 * pow(value.rgb, vec3f(0.41666)) - vec3f(0.055);
  let rgb = select(gt, lt, value.rgb <= vec3f(0.0031308));
  return vec4f(rgb, value.a);
}

@fragment fn fs_main(@location(0) uv: vec2f) -> @location(0) vec4f {
  let scene = textureSample(sceneHDR, linearSampler, uv);
  let bloom = textureSample(bloomTexture, linearSampler, uv);
  let alpha = max(scene.a, bloom.a) * u.alphaScale;
  return LinearTosRGB(vec4f(scene.rgb + bloom.rgb, alpha));
}
